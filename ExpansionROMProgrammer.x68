*LOADS A 4K ROM FILE INTO AN EXPANSION CARD F-RAM OVER SERIAL (57600 BAUD)
SCC EQU $FCD201
DATASIZE EQU $1000
BUSVCTR EQU $8
SLOTONE EQU $FC0000
SLOTTWO EQU $FC4000
SLOTTHREE EQU $FC8000
MESSAGE EQU $FE0088
MONITOR EQU $FE0084

    ORG    $800
   
START:
    MOVE.L #SCC,A1 *THE BASE ADDRESS OF THE SCC IN MEMORY
    MOVE.L #INITDATA,A2 *WHERE ALL OF THE INITIALIZATION DATA THAT NEEDS TO BE LOADED INTO THE SCC IS LOCATED
    MOVE.L #DATASIZE,D0 *LOAD THE SIZE OF THE DATA WE'RE EXPECTING TO RECEIVE (4K)
    MOVE.L BUSVCTR,A5 *SAVE THE ORIGINAL BUS ERROR VECTOR FOR LATER USE
INIT:
    MOVE.B (A2)+,(A1) *TRANSFER ALL OF THE INITIALIZATION DATA INTO THE SCC
    CMPI.B #$FF,(A2) *UNTIL WE READ AN FF (MEANING END OF THE INITIALIZATION INFO)
    BNE INIT
TESTONE:
    LEA ONEFAIL,A3 
    MOVE.L A3,BUSVCTR *WE NEED TO CHANGE THE BUS ERROR VECTOR TO POINT TO OUR ROUTINE TO HANDLE A CARD NOT BEING INSTALLED IN A SLOT. IF THE CARD IS UNABLE TO BE READ OR WRITTEN IN ANY PART OF THIS FUNCTION, THE LISA WILL GO TO THIS ERROR ROUTINE
    MOVE.L #SLOTONE,A3 *PUT THE SLOT BASE ADDRESS INTO A3
    MOVE.W (A3),D7 *AND SAVE WHATEVER'S IN THE FIRST MEMORY LOCATION IN THE (PRESUMABLY) F-RAM
    MOVE.W #$75,(A3) *TRY WRITING SOME RANDOM DATA TO THE FIRST LOCATION IN THE F-RAM
    MOVE.W (A3),D6 *READ THE DATA BACK
    MOVE.L #SLOTONE,A4
    MOVE.L A5,BUSVCTR *RESTORE THE ORIGINAL BUS ERROR VECTOR NOW THAT WE'VE DONE ALL OF THE CARD READING AND WRITING
    CMPI.B #$75,D6 *AND COMPARE THE READBACK DATA TO THE ORIGINAL WRITTEN DATA
    BNE TESTTWO *IF THEY'RE NOT THE SAME, THERE'S A CARD INSTALLED, BUT IT DOESN'T CONTAIN ANY SORT OF WRITABLE MEMORY (F-RAM), SO GO ON AND TEST THE NEXT SLOT
    MOVE.W D7,(A4) *PUT THE ORIGINAL DATA FROM THE FIRST F-RAM LOCATION BACK INTO THE F-RAM SO THAT THE CONTENTS REMAIN UNCHANGED IF THE USER ABORTS THE OPERATION
    MOVE.L #SLOTONEFOUND,A3 *LOAD THE POINTER TO THE "FOUND A CARD IN THIS SLOT" MESSAGE
    MOVE.L #12,D5
    MOVE.L #24,D6 *SET THE ROW AND COLUMN
    JSR MESSAGE *AND DISPLAY THE MESSAGE
    MOVE.L #STARTSEND,A3
    MOVE.L #13,D5
    MOVE.L #24,D6 
    JSR MESSAGE *NOW WE DO THE SAME THING TO DISPLAY A "START TRANSMITTING WHEN YOU'RE READY" MESSAGE
    JMP RECEIVE *AND GO RECEIVE THE SERIAL DATA
TESTTWO:
    LEA TWOFAIL,A3 *SAME CONCEPT AS THE SLOT ONE TEST
    MOVE.L A3,BUSVCTR
    MOVE.L #SLOTTWO,A3
    MOVE.W (A3),D7
    MOVE.W #$75,(A3)
    MOVE.W (A3),D6
    MOVE.L #SLOTTWO,A4
    MOVE.L A5,BUSVCTR
    CMPI.B #$75,D6
    BNE TESTTHREE
    MOVE.W D7,(A4)
    MOVE.L #SLOTTWOFOUND,A3
    MOVE.L #12,D5
    MOVE.L #24,D6
    JSR MESSAGE
    MOVE.L #STARTSEND,A3
    MOVE.L #13,D5
    MOVE.L #24,D6
    JSR MESSAGE
    JMP RECEIVE
TESTTHREE:
    LEA THREEFAIL,A3 *SAME CONCEPT AS THE SLOT ONE AND TWO TESTS
    MOVE.L A3,BUSVCTR
    MOVE.L #SLOTTHREE,A3
    MOVE.W (A3),D7
    MOVE.W #$75,(A3)
    MOVE.W (A3),D6
    MOVE.L #SLOTTHREE,A4
    MOVE.L A5,BUSVCTR
    CMPI.B #$75,D6
    BNE THREEFAIL
    MOVE.W D7,(A4)
    MOVE.L #SLOTTHREEFOUND,A3
    MOVE.L #12,D5
    MOVE.L #24,D6
    JSR MESSAGE
    MOVE.L #STARTSEND,A3
    MOVE.L #13,D5
    MOVE.L #24,D6
    JSR MESSAGE
    JMP RECEIVE 
RECEIVE:
    BTST.B #$0,(A1) *CHECK TO SEE IF WE HAVE ANY DATA IN THE SCC BUFFER
    BEQ RECEIVE *IF NOT, JUST LOOP UNTIL WE DO
    MOVE.B (4,A1),D1 *IF SO, MOVE THE BYTE (OFFSET BY 4 FROM THE SCC BASE ADDRESS) INTO D1
    MOVE.W D1,(A4)+ *AND THEN MOVE THE WORD FROM D1 INTO THE NEXT MEMORY LOCATION IN F-RAM. WE CAN'T MOVE STRAIGHT FROM THE SCC TO THE F-RAM SINCE WE HAVE TO READ A BYTE FROM THE SCC BUT NEED TO WRITE A WORD (ALTHOUGH THE UPPER BYTE IS IGNORED) TO THE F-RAM
    SUB.L #$1,D0 *SUBTRACT ONE FROM THE NUMBER OF BYTES LEFT TO SEND
    CMPI.L #$0,D0 *AND CHECK TO SEE IF WE'RE DONE
    BNE RECEIVE *IF NOT, WAIT FOR THE NEXT BYTE
    MOVE.L #FINISHED,A3 *IF SO, LOAD THE POINTER TO A "FINISHED!" MESSAGE
    MOVE.L #14,D5
    MOVE.L #24,D6 *SET THE ROW AND COLUMN
    JSR MESSAGE *AND DISPLAY IT TO THE USER
    MOVE.L #13,D5
    MOVE.L #24,D6 *NOW SET A ROW AND COLUMN FOR A MESSAGE FOR THE MONITOR TO DISPLAY
    MOVE.L #$0,D0 *SPECIFY THAT WE DON'T WANT THE MONITOR TO DISPLAY AN ERROR CODE
    MOVE.L #$0,A2 *OR AN ICON
    MOVE.L #SUCCESS,A3 *LOAD THE POINTER TO A "SUCCESS!" MESSAGE
    JMP MONITOR *AND EXIT TO THE MONITOR TO FINISH
INITDATA:
    DC.B $9,$C0,$B,$D0,$4,$44,$E,$1,$3,$C1,$5,$E8,$C,$0,$D,$0,$FF *THE SCC INITIALIZATION DATA (IN THE FORMAT [REGISTER NUMBER],[DATA]) WITH AN FF AT THE END TO TELL THE CODE WHEN WE'RE DONE
SLOTONEFOUND:
    DC.B 'FOUND A WRITABLE F-RAM IN SLOT ONE',0 *STATUS AND ERROR MESSAGES 
SLOTTWOFOUND:
    DC.B 'FOUND A WRITABLE F-RAM IN SLOT TWO',0
SLOTTHREEFOUND:
    DC.B 'FOUND A WRITABLE F-RAM IN SLOT THREE',0
NOSLOT:
    DC.B 'UNABLE TO FIND A CARD WITH A WRITABLE F-RAM INSTALLED',0
STARTSEND:
    DC.B 'START TRANSFER WHEN READY...',0
FINISHED:
    DC.B 'DONE',0
SUCCESS:
    DC.B 'SUCCESS...',0 
ONEFAIL:
    JMP TESTTWO *IF WE GET A BUS ERROR ON SLOT ONE (NO CARD INSTALLED), GO ON AND TRY SLOT TWO
TWOFAIL:
    JMP TESTTHREE *IF THERE'S A BUS ERROR ON SLOT TWO, TRY SLOT THREE
THREEFAIL:
    MOVE.L #NOSLOT,A3 *IF THERE ISN'T A CARD IN SLOT THREE EITHER (OR THE CARD JUST ISN'T WRITABLE), LOAD THE POINTER TO A "NO VALID CARD FOUND" MESSAGE
    MOVE.L #12,D5
    MOVE.L #24,D6 *SET THE ROW AND COLUMN
    MOVE.L #$0,D0 *TELL THE MONITOR NOT TO SHOW AN ERROR CODE
    MOVE.L #$0,A2 *OR AN ICON
    JMP MONITOR *AND EXIT TO THE MONITOR
    END THREEFAIL
   



*~Font name~Courier New~
*~Font size~10~
*~Tab type~1~
*~Tab size~4~
